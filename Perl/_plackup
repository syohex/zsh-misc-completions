#compdef plackup

# The MIT License (MIT)
#
# Copyright (c) 2022 Shohei YOSHIDA
#
# Permission is hereby granted, free of charge, to any person obtaining a copy
# of this software and associated documentation files (the "Software"), to deal
# in the Software without restriction, including without limitation the rights
# to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
# copies of the Software, and to permit persons to whom the Software is
# furnished to do so, subject to the following conditions:
#
# The above copyright notice and this permission notice shall be included in
# all copies or substantial portions of the Software.
#
# THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
# IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
# FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
# AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
# LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
# OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
# THE SOFTWARE.
#
# ------------------------------------------------------------------------------
# Description
# -----------
#
#  Completion script for plackup 1.0050 (https://github.com/plack/Plack)
#
# ------------------------------------------------------------------------------

local psgi_server_dir=$(dirname $(perldoc -l Plack::Handler::Standalone))
local -a psgi_servers=(${(@f)"$(echo $psgi_server_dir/*.pm(.:t:r))"})

_arguments \
  '(-a --app)'{-a,--app}'[locate a .psgi script]: :_files -g "*.psgi"' \
  '-e[evaluate the given code as a PSGI app]:code' \
  '(-o --host)'{-o,--host}'[Binds to a TCP interface]:host' \
  '(-p --port)'{-p,--port}'[Binds to TCP port. Defaults to 5000]:port' \
  '(-s --server)'{-s,--server}'[Selects a specific server implementation to run on]: :'"($psgi_servers)" \
  '(-S --socket)'{-S,--socket}'[Listens on a UNIX domain socket file]: :_files' \
  \*{-l,--listen}'[Listens on one or more addresses whether "HOST:PORT", ":PORT" or path]: :_files' \
  '(-D --daemonize)'{-D,--daemonize}'[makes the process go background]' \
  '*-I[Specifies Perl library include paths]: :_files -/' \
  '*-M-[specify modules to load before loading the app code]:module:_perl_modules -S= -q' \
  \*{-E,--env}'[Specifies the environment option]:env' \
  '(--no-default-middleware)--no-default-middleware[prevents loading middleware stack]' \
  '(-r --reload)'{-r,--reload}'[Makes plackup restart the server whenever a file in your development directory changes]' \
  '(-R --Reload)'{-R,--Reload}'[Makes plackup restart the server whenever a file in any of the given directories changes]: :_files -/' \
  '(-L --Loader)'{-L,--Loader}'[Specifies the server loading subclass that implements how to run the server]:subclass' \
  '--access-log[Specifies the pathname of a file where the access log should be written]:_files' \
  '--path[Specify the root path of application to run]: :_files -/' \
  '(- :)'{-h,--help}'[Show help]' \
  '(- :)--version[Show version]' \
  '*: :_files -g "*.psgi"' && return 0

# Local Variables:
# mode: Shell-Script
# sh-indentation: 2
# indent-tabs-mode: nil
# sh-basic-offset: 2
# End:
# vim: ft=zsh sw=2 ts=2 et
