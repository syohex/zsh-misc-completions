#compdef carmel

# The MIT License (MIT)
#
# Copyright (c) 2022 Shohei YOSHIDA
#
# Permission is hereby granted, free of charge, to any person obtaining a copy
# of this software and associated documentation files (the "Software"), to deal
# in the Software without restriction, including without limitation the rights
# to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
# copies of the Software, and to permit persons to whom the Software is
# furnished to do so, subject to the following conditions:
#
# The above copyright notice and this permission notice shall be included in
# all copies or substantial portions of the Software.
#
# THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
# IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
# FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
# AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
# LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
# OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
# THE SOFTWARE.
#
# ------------------------------------------------------------------------------
# Description
# -----------
#
#  Completion script for carmel v0.1.56 (https://github.com/miyagawa/Carmel)
#
# ------------------------------------------------------------------------------

_carmel() {
  typeset -A opt_args
  local context state line
  local ret=1

  _arguments -C \
    '(- *)'{-h,--help}'[Show help message]' \
    '(- *)--version[Show version and exit]' \
    '(-v --verbose)'{-v,--verbose}'[Enable debug mode]' \
    '1: :_carmel_subcommands' \
    '*:: :->args' && ret=0

  case $state in
    (args)
      case $words[1] in
        (exec)
          _arguments -s : "$opts[@]" '*::arguments:_normal' && ret=0
        ;;
        (*)
          ret=0
          ;;
      esac
  esac

  return $ret
}

(( $+functions[_carmel_subcommands] )) ||
_carmel_subcommands() {
  local -a commands=(
    "install:Install dependencies"
    "export:Generate export environments variables for shell scripting"
    "env:Show related environment variables(PATH, PERL5OPT)"
    "find:Find module in a repository"
    "show:Show module location"
    "list:List all the modules to be loaded"
    "update:Update modules"
    "diff:Show diffs for cpanfile.snapshot"
    "tree:List all the modules in a tree"
    "exec:Run your perl script with modules from artifacts"
    "rollout:Roll out the currently selected modules into ./local"
    "package:Package modules tarballs and index into ./vendor/cache"
    "help:Show help"
    "version:Show version"
  )

  _describe -t subcommands "subcommands" commands
}

_carmel "$@"

# Local Variables:
# mode: Shell-Script
# sh-indentation: 2
# indent-tabs-mode: nil
# sh-basic-offset: 2
# End:
# vim: ft=zsh sw=2 ts=2 et
