#compdef claude

# The MIT License (MIT)
#
# Copyright (c) 2026 Shohei YOSHIDA
#
# Permission is hereby granted, free of charge, to any person obtaining a copy
# of this software and associated documentation files (the "Software"), to deal
# in the Software without restriction, including without limitation the rights
# to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
# copies of the Software, and to permit persons to whom the Software is
# furnished to do so, subject to the following conditions:
#
# The above copyright notice and this permission notice shall be included in
# all copies or substantial portions of the Software.
#
# THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
# IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
# FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
# AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
# LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
# OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
# THE SOFTWARE.
#
# ------------------------------------------------------------------------------
# Description
# -----------
#
#  Completion script for claude code 2.1.15 (https://github.com/anthropics/claude-code)
#
# ------------------------------------------------------------------------------

_claude() {
  local context state state_descr line
  typeset -A opt_args
  local ret=1

  _arguments -C \
    '--add-dir[Additional directories to allow tool access to]:dir:_files -/' \
    '--agent[Agent for the current session]:agent' \
    '--agents[JSON object defining custom agents]:json' \
    '--allow-dangerously-skip-permissions[Enable bypassing all permission checks as an option]' \
    '--allowed-tools[Comma or space-separated list of tool names to allow]:tools' \
    '--append-system-prompt[Append a system prompt to the default system prompt]:prompt' \
    '--betas[Beta headers to include in API requests]:beta' \
    '(--chrome --no-chrome)--chrome[Enable Claude in Chrome integration]' \
    '(-c --continue)'{-c,--continue}'[Continue the most recent conversation]' \
    '--dangerously-skip-permissions[Bypass all permission checks]' \
    '(-d --debug)'{-d,--debug}'[Enable debug mode with optional category filtering]:filter' \
    '--disable-slash-commands[Disable all slash commands]' \
    '--disallowed-tools[Comma or space-separated list of tool names to deny]:tools' \
    '--fallback-model[Enable automatic fallback to specified model]:model' \
    '--file[File resources to download at startup]:file' \
    '--fork-session[When resuming, create a new session ID instead of reusing the original]' \
    '(- *)'{-h,--help}'[Display help for command]' \
    '--ide[Automatically connect to IDE on startup]' \
    '--include-partial-messages[Include partial message chunks as they arrive]' \
    '--input-format[Input format]:format:(text stream-json)' \
    '--json-schema[JSON Schema for structured output validation]:schema' \
    '--max-budget-usd[Maximum dollar amount to spend on API calls]:amount' \
    '--mcp-config[Load MCP servers from JSON files or strings]:json_or_string:_files -g "*.json"' \
    '--model[Model for the current session]:model:(sonnet opus)' \
    '(--chrome --no-chrome)--no-chrome[Disable Claude in Chrome integration]' \
    '--no-session-persistence[Disable session persistence]' \
    '--output-format[Output format]:format:(text json stream-json)' \
    '--permission-mode[Permission mode to use for the session]:mode:(acceptEdits bypassPermissions default delegate dontAsk plan)' \
    *'--plugin-dir[Load plugins from directories for this session only]:dir:_files -/' \
    '(-p --print)'{-p,--print}'[Print response and exit]' \
    '--replay-user-message[Re-emit user messages from stdin back on stdout for acknowledgment]' \
    '(-r --resume)'{-r,--resume}'[Resume a conversation by session ID]:session_id' \
    '--session-id[Use a specific session ID for the conversation]:uuid' \
    '--setting-sources[Comma-separated list of setting sources to load]:source:_claude_setting_sources' \
    '--settings[Path to a settings JSON file or a JSON string to load additional settings from]:file_or_json:_files -g "*.json"' \
    '--strict-mcp-config[Only use MCP servers from --mcp-config]' \
    '--system-prompt[System prompt to use for the session]:prompt' \
    '--tools[Specify the list of available tools from the built-in set]:tools' \
    '--verbose[Override verbose mode setting from config]' \
    '(- *)'{-v,--version}'[Output the version number]' \
    '1: :_claude_commands' \
    '*:: :->subcmd' && ret=0

  case $state in
    (subcmd)
      case $words[1] in
        (install)
          _arguments \
            '--force[Force installation even if already installed]' \
            '(- *)'{-h,--help}'[Display help for command]' \
            '1:target:(stable latest)' \
            && ret=0
          ;;
        (mcp)
          _claude_mcp && ret=0
          ;;
        (plugin)
          _claude_plugin && ret=0
          ;;
        (*)
          _arguments \
            '(- *)'{-h,--help}'[Display help for command]' \
            && ret=0
          ;;
      esac
    ;;
  esac

  return ret
}

(( $+functions[_claude_commands] )) ||
_claude_commands() {
  local -a commands=(
    "doctor:Check the health of your Claude Code auto-updater"
    "install:Install Claude Code native build"
    "mcp:Configure and manage MCP servers"
    "plugin:Manage Claude Code plugins"
    "setup-token:Set up a long-lived authentication token"
    "update:Check for updates and install if available"
  )

  _describe -t 'commands' "commands" commands
}

(( $+functions[_claude_mcp] )) ||
_claude_mcp() {
  local ret=1

  local -a commands=(
    'add-from-claude-desktop:Import MCP servers from Claude Desktop'
    'add-json:Add an MCP server with a JSON string'
    'get:Get details about an MCP server'
    'help:display help for command'
    'list:List configured MCP servers'
    'remove:Remove an MCP server'
    'reset-project-choices:Reset all approved and rejected project-scoped servers within this project'
    'serve:Start the Claude Code MCP server'
  )

  _arguments -C \
    '(- : *)'{-h,--help}'[Show command line help]' \
    '1: :(($commands))' \
    '*:: :->subcmd' && ret=0

  case $state in
    (subcmd)
      case $words[1] in
        (add-from-claude-desktop|add-json|remove)
          _arguments \
            '(- *)'{-h,--help}'[Display help for command]' \
            '(-s --scope)'{-s,--scope}'[Configuration scope(default: local)]:scope:(local user project)' \
            '1:name:_claude_mcp_names' \
            '*:: :_files'
          ;;
        (get)
          _arguments \
            '(- *)'{-h,--help}'[Display help for command]' \
            '*:: :_claude_mcp_names' \
            && ret=0
          ;;
        (help)
          local -a help_commands=(
            add-from-claude-desktop add-json get help list remove reset-project-choices serve
          )
          _arguments \
            '1:command:(($help_commands))' \
            && ret=0
          ;;
        (serve)
          _arguments \
            '(-d --debug)'{-d,--debug}'[Enable debug mode]' \
            '(- *)'{-h,--help}'[Display help for command]' \
            '--verbose[Override verbose mode setting from config]' \
            && ret=0
          ;;
        (*)
          _arguments \
            '(- *)'{-h,--help}'[Display help for command]' \
            && ret=0
          ;;
      esac
    ;;
  esac

  return ret
}

(( $+functions[_claude_plugin] )) ||
_claude_plugin() {
  local ret=1

  local -a commands=(
    'disable:Disable an enabled plugin'
    'enable:Enable a disabled plugin'
    'help:display help for command'
    'install:Install a plugin from available marketplaces'
    'list:List installed plugins'
    'marketplace:Manage Claude Code marketplaces'
    'uninstall:Uninstall an installed plugin'
    'update:Update a plugin to the latest version'
    'validate:Validate a plugin or market manifest'
  )

  _arguments -C \
    '(- *)'{-h,--help}'[Show help and usage information]' \
    '1: :(($commands))' \
    '*:: :->subcmd' && ret=0

  case $state in
    (subcmd)
      case $words[1] in
        (disable|enable|install|uninstall|update)
          _arguments \
            '(- *)'{-h,--help}'[Display help for command]' \
            '(-s --scope)'{-s,--scope}'[Installation scope(default user)]:scope:(user project local)' \
            && ret=0
          ;;
        (help)
          local -a help_commands=(disable enable help install list marketplace uninstall update validate)
          _arguments \
            '1:command:(($help_commands))' \
            && ret=0
          ;;
        (list)
          _arguments \
            '--available[Include available plugins from marketplaces(requires --json)]' \
            '(- *)'{-h,--help}'[Display help for command]' \
            '--json[Output as JSON]' \
            && ret=0
          ;;
        (marketplace)
          _claude_plugin_marketplace && ret=0
          ;;
        (*)
          _arguments \
            '(- *)'{-h,--help}'[Show command line help]' \
            && ret=0
          ;;
      esac
      ;;
  esac

  return ret
}

(( $+functions[_claude_plugin_marketplace] )) ||
_claude_plugin_marketplace() {
  local ret=1

  local -a commands=(
    'add:Add a marketplace from a URL, path, or GitHub repo'
    'help:display help for command'
    'list:List all configured marketplaces'
    'remove:Remove a configured marketplace'
    'update:Update marketplace(s) from their source or update all'
  )

  _arguments -C \
    '(- *)'{-h,--help}'[Show help and usage information]' \
    '1: :(($commands))' \
    '*:: :->subcmd' && ret=0

  case $state in
    (subcmd)
      case $words[1] in
        (help)
          local -a help_commands=(add help list remove update)
          _arguments \
            '1:command:(($help_commands))' \
            && ret=0
          ;;
        (list)
          _arguments \
            '(- *)'{-h,--help}'[Display help for command]' \
            '--json[Output as JSON]' \
            && ret=0
          ;;
        (*)
          _arguments \
            '(- *)'{-h,--help}'[Display help for command]' \
            '*:: :_files' \
            && ret=0
          ;;
      esac
      ;;
  esac

  return ret
}

# Utilities
(( $+functions[_claude_setting_sources] )) ||
_claude_setting_sources() {
  local -a sources=(user project local)
  _values -S ',' sources sources
}

(( $+functions[_claude_mcp_names] )) ||
_claude_mcp_names() {
  if [[ -e ~/.claude.json ]]; then
    local -a names=(
      $(jq -r '[.projects[]?.mcpServers? // {} | keys[]] | unique | .[] | select(. != "" and . != null)' ~/.claude.json)
      )
    _values "mcp_names" $names
  fi
}

_claude "$@"

# Local Variables:
# mode: Shell-Script
# sh-indentation: 2
# indent-tabs-mode: nil
# sh-basic-offset: 2
# End:
# vim: ft=zsh sw=2 ts=2 et
